buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.2.3'
    }
}

allprojects {
    repositories {
        jcenter()
    }
    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }
}

apply plugin: 'com.android.application'

def VERSION_CODE = 1
def VERSION_NAME = "1.00.00"
def PACKAGE_NAME = "jp.co.e2.baseapplication"

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.2"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 21
        versionCode VERSION_CODE
    }

    buildTypes {
        debug {
            //デバッグモードON
            debuggable true
            //apk最適化
            zipAlignEnabled true
            //パッケージ名のサフィックス
            applicationIdSuffix ".debug"
            // Version名につけるサフィックス
            versionNameSuffix '-debug'
        }
        release {
            //デバッグモードOFF
            debuggable false
            //apk最適化
            zipAlignEnabled true
            //難読化
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
    }

    productFlavors {
        develop {
            applicationId "${PACKAGE_NAME}.develop"
            versionName "${VERSION_NAME}-develop"
        }
        staging {
            applicationId "${PACKAGE_NAME}.staging"
            versionName "${VERSION_NAME}-staging"
        }
        production {
            applicationId PACKAGE_NAME
            versionName VERSION_NAME
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: '*.jar')
    compile 'net.arnx:jsonic:1.2.9'
    compile 'org.apache.httpcomponents:httpcore:4.3.2'
    compile 'org.apache.httpcomponents:httpmime:4.3.5'
    compile 'com.android.support:support-v4:22.2.0'
    compile "com.android.support:cardview-v7:+"
    compile 'com.android.support:appcompat-v7:22.2.0'
}